[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
eclipse.project.name = appName + '-util'

dependencies {
  api "com.badlogicgames.gdx:gdx:$gdxVersion"
  api "com.badlogicgames.gdx:gdx-tools:$gdxVersion"

  if(enableGraalNative == 'true') {
    implementation "io.github.berstanio:gdx-svmhelper-annotations:$graalHelperVersion"
  }
}

tasks.register("validateQuestContent", JavaExec) {
  group = "verification"
  description = "Validates quest and dialog yarn content for missing headers and references."
  classpath = sourceSets.main.runtimeClasspath
  mainClass = "com.github.tilcob.game.ContentValidator"
  args = [
    project.rootDir.toPath().resolve("assets").toString(),
    project.rootDir.toPath().resolve("assets").resolve("assets/definitions.commands.ysls.json").toString()
  ]

  inputs.files(
    fileTree(project.rootDir.toPath().resolve("assets").resolve("quests")) { include "**/*.yarn", "index.json" },
    fileTree(project.rootDir.toPath().resolve("assets").resolve("dialogs")) { include "**/*.yarn", "index.json" },
    fileTree(project.rootDir.toPath().resolve("assets").resolve("cutscenes")) { include "**/*.yarn", "index.json" }
  )
}
