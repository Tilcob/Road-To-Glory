import groovy.json.JsonOutput

buildscript {
  repositories {
    mavenCentral()
    gradlePluginPortal()
    if (useMavenLocal == 'true') {
      mavenLocal()
    }
    google()
    if (useSnapshots == 'true') {
      maven { url = 'https://central.sonatype.com/repository/maven-snapshots/' }
    }
  }
  dependencies {
  }
}

allprojects {
  apply plugin: 'eclipse'
  apply plugin: 'idea'
  apply plugin: 'base'

  // This allows you to "Build and run using IntelliJ IDEA", an option in IDEA's Settings.
  idea {
    module {
      outputDir = file('build/classes/java/main')
      testOutputDir = file('build/classes/java/test')
    }
  }
  repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
  }
}

configure(subprojects) {
  apply plugin: 'java-library'
  java.sourceCompatibility = 17

  if (enableDependencyLocking == 'true') {
    dependencyLocking {
      lockAllConfigurations()
    }
  }

  // From https://lyze.dev/2021/04/29/libGDX-Internal-Assets-List/
  // The article can be helpful when using assets.txt in your project.
  tasks.register('generateAssetList') {
    inputs.dir("${project.rootDir}/assets/")
    // projectFolder/assets
    File assetsFolder = new File("${project.rootDir}/assets/")
    // projectFolder/assets/assets.txt
    File assetsFile = new File(assetsFolder, "assets.txt")
    // delete that file in case we've already created it
    assetsFile.delete()

    // iterate through all files inside that folder
    // convert it to a relative path
    // and append it to the file assets.txt
    fileTree(assetsFolder).collect { assetsFolder.relativePath(it) }.sort().each {
      assetsFile.append(it + "\n")
    }
  }
  if (generateAssetList == 'true') {
    processResources.dependsOn 'generateAssetList'
  }

  compileJava {
    options.incremental = true
  }
}

tasks.register("genQuestIndex") {
  group = "assets"
  description = "Generates assets/quests/index.json from *.yarn."
  doLast {
    def dir = file("$rootDir/assets/quests")
    if (!dir.exists()) throw new GradleException("Directory not found: $dir")

    def outFile = file("$rootDir/assets/quests/index.json")

    def names = dir.listFiles()
      .findAll { it.isFile() && it.name.toLowerCase().endsWith("yarn") }
      .collect { it.name }
      .findAll { !it.contains("test") }
      .sort()

    outFile.text = JsonOutput.prettyPrint(JsonOutput.toJson(names)) + "\n"
    println "Generated $outFile (${names.size()} entries)"
  }
}

tasks.register("genQuestTestIndex") {
  group = "assets"
  description = "Generates assets/tests/quests_test/index.json from *.yarn."
  doLast {
    def dir = file("$rootDir/assets/tests/quests_test")
    if (!dir.exists()) throw new GradleException("Directory not found: $dir")

    def outFile = file("$rootDir/assets/tests/quests_test/index.json")

    def names = dir.listFiles()
      .findAll { it.isFile() && it.name.toLowerCase().endsWith("yarn") }
      .collect { it.name }
      .sort()

    outFile.text = JsonOutput.prettyPrint(JsonOutput.toJson(names)) + "\n"
    println "Generated $outFile (${names.size()} entries)"
  }
}

tasks.register("genDialogIndex") {
  group = "assets"
  description = "Generates assets/dialogs/index.json from *.yarn."
  doLast {
    def dir = file("$rootDir/assets/dialogs")
    if (!dir.exists()) throw new GradleException("Directory not found: $dir")

    def outFile = file("$rootDir/assets/dialogs/index.json")

    def names = dir.listFiles()
      .findAll { it.isFile() }
      .collect { it.name }
      .findAll { it.toLowerCase().endsWith(".yarn") }
      .sort()

    outFile.text = JsonOutput.prettyPrint(JsonOutput.toJson(names)) + "\n"
    println "Generated $outFile (${names.size()} entries)"
  }
}

tasks.register("genItemIndex") {
  group = "assets"
  description = "Generates assets/items/index.json from *.json (excluding index.json)."
  doLast {
    def dir = file("$rootDir/assets/items")
    if (!dir.exists()) {
      println "Items directory not found, skipping: $dir"
      return
    }

    def outFile = file("$rootDir/assets/items/index.json")

    def names = dir.listFiles()
      .findAll { it.isFile() }
      .collect { it.name }
      .findAll { it.toLowerCase().endsWith(".json") }
      .findAll { it != "index.json" }
      .sort()

    outFile.text = JsonOutput.prettyPrint(JsonOutput.toJson(names)) + "\n"
    println "Generated $outFile (${names.size()} entries)"
  }
}

tasks.register("genCutsceneIndex") {
  group = "assets"
  description = "Generates assets/cutscenes/index.json from *.yarn."
  doLast {
    def dir = file("$rootDir/assets/cutscenes")
    if (!dir.exists()) throw new GradleException("Directory not found: $dir")

    def outFile = file("$rootDir/assets/cutscenes/index.json")

    def names = dir.listFiles()
      .findAll { it.isFile() }
      .collect { it.name }
      .findAll { it.toLowerCase().endsWith(".yarn") }
      .sort()

    outFile.text = JsonOutput.prettyPrint(JsonOutput.toJson(names)) + "\n"
    println "Generated $outFile (${names.size()} entries)"
  }
}

def genIndex = { String dirPath, List<String> exts ->
  return {
    def dir = file("$rootDir/assets/$dirPath")
    if (!dir.exists()) throw new GradleException("Directory not found: $dir")

    def outFile = file("$rootDir/assets/$dirPath/index.json")

    def names = dir.listFiles()
      .findAll { it.isFile() }
      .collect { it.name }
      .findAll { n -> exts.any { n.toLowerCase().endsWith(it) } }
      .findAll { it != "index.json" }
      .sort()

    outFile.text = JsonOutput.prettyPrint(JsonOutput.toJson(names)) + "\n"
    println "Generated $outFile (${names.size()} entries)"
  }
}

tasks.register("genGraphicsIndex") {
  group = "assets"
  description = "Generates assets/graphics/index.json from *.atlas."
  doLast genIndex("graphics", [".atlas"])
}

tasks.register("genAudioIndex") {
  group = "assets"
  description = "Generates assets/audio/index.json from *.wav/*.ogg/*.mp3."
  doLast genIndex("audio", [".wav", ".ogg", ".mp3"])
}

tasks.register("genUiIndex") {
  group = "assets"
  description = "Generates assets/ui/index.json from skin.atlas + skin.json (filtered)."
  doLast genIndex("ui", [".atlas", ".json"])
}

tasks.register("genMapsIndex") {
  group = "assets"
  description = "Generates assets/maps/index.json from *.tmx."
  doLast genIndex("maps", [".tmx"])
}

tasks.register("genAssetIndexes") {
  group = "assets"
  description = "Generates quest + dialog + items + cutscenes index files."
  dependsOn(
    "genQuestIndex", "genDialogIndex", "genItemIndex",
    "genQuestTestIndex", "genCutsceneIndex", "genGraphicsIndex",
    "genAudioIndex", "genUiIndex", "genMapsIndex"
  )
}

// Automatically run before processResources in all subprojects (core/lwjgl3/etc.)
subprojects {
  tasks.matching { it.name == "processResources" }.configureEach {
    dependsOn(rootProject.tasks.named("genAssetIndexes"))
  }
}


subprojects {
  version = "$projectVersion"
  ext.appName = 'Road To Glory'
  repositories {
    mavenCentral()
    // You may want to remove the following line if you have errors downloading dependencies.
    if (useMavenLocal == 'true') {
      mavenLocal()
    }
    if (useSnapshots == 'true') {
      maven { url = 'https://central.sonatype.com/repository/maven-snapshots/' }
    }
    maven { url = 'https://jitpack.io' }
  }
}

tasks.named("check") {
  dependsOn(":utils:validateQuestContent")
}

project(":lwjgl3") {
  tasks.matching { it.name == "run" }.configureEach {
    dependsOn(":utils:validateQuestContent")
  }
  tasks.matching { it.name == "build" }.configureEach {
    dependsOn(":utils:validateQuestContent")
  }
}



eclipse.project.name = 'Road To Glory' + '-parent'
